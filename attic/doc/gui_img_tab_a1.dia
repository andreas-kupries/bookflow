# -*- tcl -*- tcl.tk//DSL diagram//EN//1.0

######################################################################

proc portrait {h args} {
    box height $h width [expr {0.75*$h}] {*}$args
}

proc landscape {w args} {
    box width $w height [expr {0.75*$w}] {*}$args
}

proc thumb {args} {
    landscape [32 mm] "Thumb" {*}$args
}

proc sthumb {args} {
    thumb {*}$args stroke 3
}

proc ellipsis {} {
    move same ; circle rad [1 mm] fillcolor black
    move same ; circle same
    move same ; circle same
}

proc iseries {args} {
    block {
	box width [12 cm] height [9 cm]
	block {
	    east              ; thumb
	    move right [2 mm] ; sthumb
	    ellipsis
	} with nw at [[[last box] nw] by [5 mm] se]
	block {
	    east              ; ellipsis
	    move right [2 mm] ; thumb
	    move right [2 mm] ; thumb
	} with se at [[[last box] se] by [5 mm] nw]
    } {*}$args
}

proc wrap {e} {
    # e = element to wrap.

    set x [[arc rad [5 mm] from [[$e sw] by [5 mm] left]] start]
    line right [$e width]
    arc rad [5 mm]
    line up [$e height]
    arc rad [5 mm]
    line left [$e width]
    arc rad [5 mm]
    tab Images
    tabA  {Book 1}
    tabA ...
    tabA {Book N}
    line to $x
}

proc tab {{text {}}} {
    arc rad [5 mm] cw ; line ; tablabel $text
    arc rad [5 mm]    ; line down [5 mm]
    arc rad [5 mm]    ; line
    arc rad [5 mm] cw
    return
}
proc tabB {{text {}}} {
    group {
	arc rad [5 mm] cw ; line ; tablabel $text
	arc rad [5 mm]    ; line down [5 mm]
	arc rad [5 mm]
    }
    line down [15 mm]
}

proc tabA {{text {}}} {
    group {
	west
	arc rad [5 mm] from [[2nd last arc] end]
	line down [5 mm]
	arc rad [5 mm] ; line ; tablabel $text up
	arc rad [5 mm] cw
    }
}

proc tablabel {text {dir down}} {
    if {$text eq {}} return
    group {
	text text $text with c at [[[last line] c] by [7.5 mm] $dir]
    }
    return
}

######################################################################

text "Notebook Page \"Image Series\" (Alternative I)"
move south [1 cm]
wrap [iseries]
move

